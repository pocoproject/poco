import org.pocoproject.nativeplatform.test.cppunit.CppUnitTestSuiteSpec

model {
	components {
		withType(NativeComponentSpec) {
			binaries.withType(NativeBinarySpec) {
				if (buildType == buildTypes.debug) {
					if (it instanceof NativeExecutableBinarySpec) {
						executable.file = toLocalBin(appendDebugSuffix(executable.file), targetPlatform)
					}
				} else
				if (buildType == buildTypes.release) {
					if (it instanceof NativeExecutableBinarySpec) {
						executable.file = toLocalBin(executable.file, targetPlatform)
					} 
				}
			}
		}
		TestSuite(NativeExecutableSpec) { m ->
			sources {
				cpp {
					source {
						srcDir 'src'
						include '**/*.cpp'
						exclude 'Win*Driver.cpp'
						exclude 'Win*.cpp'
					}
					exportedHeaders {
						srcDir 'src'
					}
					lib project: ':CppUnit', library: 'CppUnit', linkage: 'shared'
					lib project: ':JSON', library: 'JSON', linkage: 'shared'
					lib project: ':XML', library: 'XML', linkage: 'shared'
					lib project: ':Util', library: 'Util', linkage: 'shared'
					lib project: ':Foundation', library: 'Foundation', linkage: 'shared'
				}
			}
			binaries.all {
				if (targetPlatform.operatingSystem.windows) {
					sources {
						platformWindows(CppSourceSet) {
							lib m.sources.cpp
							source.srcDir 'src'
							source.include 'Win*.cpp'
						}
					}
				}
			}
		}
	}
	testSuites {
		UtilTestSuite(org.pocoproject.nativeplatform.test.cppunit.CppUnitTestSuiteSpec) {
			testing $.components.TestSuite 
		}
	}
}
tasks.withType(RunTestExecutable) {
	args "-print"
}


