set(LIBNAME "Util")
set(POCO_LIBNAME "Poco${LIBNAME}")

# Sources
file(GLOB SRCS_G "src/*.cpp")
POCO_SOURCES_AUTO( SRCS ${SRCS_G})

# Headers
file(GLOB_RECURSE HDRS_G "include/*.h" )
POCO_HEADERS_AUTO( SRCS ${HDRS_G})

POCO_SOURCES_AUTO_PLAT( SRCS WIN32
    src/WinRegistryConfiguration.cpp
    src/WinRegistryKey.cpp
    src/WinService.cpp
)

add_library( "${LIBNAME}" ${SRCS} )
add_library( "${POCO_LIBNAME}" ALIAS "${LIBNAME}")
set_target_properties( "${LIBNAME}"
    PROPERTIES
    VERSION ${SHARED_LIBRARY_VERSION} SOVERSION ${SHARED_LIBRARY_VERSION}
    OUTPUT_NAME ${POCO_LIBNAME}
    DEFINE_SYMBOL Util_EXPORTS
    )

target_link_libraries( "${LIBNAME}" Foundation)
if (POCO_ENABLE_XML)
	target_link_libraries( "${LIBNAME}" XML)
else ()
	add_definitions( -DPOCO_UTIL_NO_XMLCONFIGURATION )
endif()
if (POCO_ENABLE_JSON)
	target_link_libraries( "${LIBNAME}" JSON)
else ()
	add_definitions( -DPOCO_UTIL_NO_JSONCONFIGURATION )
endif()

target_include_directories( "${LIBNAME}"
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
        $<INSTALL_INTERFACE:include>
    PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/src
    )
target_compile_definitions("${LIBNAME}" PUBLIC ${LIB_MODE_DEFINITIONS})

POCO_INSTALL("${LIBNAME}")
POCO_GENERATE_PACKAGE("${LIBNAME}")

if (POCO_ENABLE_TESTS)
    add_subdirectory(testsuite)
endif ()
if (POCO_ENABLE_SAMPLES)
    add_subdirectory(samples)
endif ()
